/*
 * Copyright OpenSearch Contributors
 * SPDX-License-Identifier: Apache-2.0
 */

import org.opensearch.gradle.test.RestIntegTestTask


buildscript {
ext {
	opensearch_group = "org.opensearch"
	opensearch_version = System.getProperty("opensearch.version", "3.4.0-SNAPSHOT")
}

repositories {
	mavenLocal()
	maven { url = "https://aws.oss.sonatype.org/content/repositories/snapshots" }
	maven { url "https://ci.opensearch.org/ci/dbc/snapshots/maven/" }
	mavenCentral()
	maven { url = "https://plugins.gradle.org/m2/" }
}

dependencies {
	classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
}
}

plugins {
id 'java'
id 'idea'
id 'eclipse'
id 'org.graalvm.buildtools.native' version '0.11.3'
id 'org.graalvm.python' version '25.0.1'
id 'com.diffplug.spotless' version '8.1.0'
}

apply plugin: 'opensearch.yaml-rest-test'
apply plugin: 'opensearch.opensearchplugin'
apply plugin: 'opensearch.pluginzip'
apply plugin: 'opensearch.java-agent'

allprojects {
group = 'org.opensearch'
version = opensearch_version - "-SNAPSHOT" + ".0"
}

ext {
graalVersion = '25.0.1'
antlr4Version = '4.13.2'
compilerVersion = '25.0.1'
}

dependencies {
// org.graalvm.python:python and org.graalvm.python:python-embedding are injected automatically by plugin org.graalvm.python.
// See https://www.graalvm.org/python/docs/#gradle-plugin-configuration
implementation "org.graalvm.polyglot:polyglot:$graalVersion"
implementation "org.graalvm.python:python-resources:$graalVersion" // Core Python stdlib and resources
implementation "org.graalvm.sdk:graal-sdk:$graalVersion"
implementation "org.antlr:antlr4-runtime:$antlr4Version"
implementation 'org.projectlombok:lombok:1.18.38'
}

def pluginName = 'lang-python'
def pluginDescription = 'Python as a scripting language for OpenSearch'
def packagePath = 'org.opensearch'
def pathToPlugin = 'python'
def pluginClassName = 'PythonModulePlugin'
def pythonLauncherLibDir = layout.buildDirectory.dir('libs')
def appendModuleNames = "org.graalvm.polyglot";
forbiddenPatterns.enabled = false

ext {
// Do not fail on `javadoc` warning (ANTLR generated code)
failOnJavadocWarning = false
}

// This is to generate a plugin-descriptor.properties file that is required by all opensearch plugins
// see https://github.com/opensearch-project/opensearch-plugins/blob/main/BUILDING.md#plugin-descriptorproperties
opensearchplugin {
name = pluginName
description = pluginDescription
classname = "${packagePath}.${pathToPlugin}.${pluginClassName}"
licenseFile = rootProject.file('LICENSE.txt')
noticeFile = rootProject.file('NOTICE.txt')
}

publishing {
publications {
	pluginZip(MavenPublication) { publication ->
	pom {
		name = pluginName
		description = pluginDescription
	}
	}
}
}

// This requires an additional Jar not published as part of build-tools
loggerUsageCheck.enabled = false

// No need to validate pom, as we do not upload to maven/sonatype
validateNebulaPom.enabled = false

repositories {
mavenLocal()
maven { url = "https://aws.oss.sonatype.org/content/repositories/snapshots" }
maven { url "https://ci.opensearch.org/ci/dbc/snapshots/maven/" }
mavenCentral()
maven { url = "https://plugins.gradle.org/m2/" }
}

test {
include '**/*Tests.class'
}

tasks.register('integTest', RestIntegTestTask) {
description = "Run tests against a cluster"
testClassesDirs = sourceSets.test.output.classesDirs
classpath = sourceSets.test.runtimeClasspath
}
tasks.named("check").configure { dependsOn("integTest") }

tasks.named("publishNebulaPublicationToMavenLocal").configure { dependsOn "generatePomFileForPluginZipPublication" }

integTest {
dependsOn 'copyDependencies', 'downloadGraalCompiler'
// The --debug-jvm command-line option makes the cluster debuggable; this makes the tests debuggable
if (System.getProperty("test.debug") != null) {
	jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=*:5005'
}
}

testClusters.integTest {
testDistribution = "INTEG_TEST"

// This installs our plugin into the testClusters
plugin(project.tasks.bundlePlugin.archiveFile)
jvmArgs "--module-path=${pythonLauncherLibDir.get().asFile} --add-modules=${appendModuleNames} -XX:+UnlockExperimentalVMOptions -XX:+EnableJVMCI --upgrade-module-path=${layout.buildDirectory.file("compiler.jar").get().asFile} --enable-native-access=org.graalvm.truffle,ALL-UNNAMED -Dtruffle.class.path.append=${pythonLauncherLibDir.get().asFile}"
}

run {
useCluster testClusters.integTest
}

// updateVersion: Task to auto update version to the next development iteration
tasks.register('updateVersion') {
onlyIf { System.getProperty('newVersion') }
doLast {
	ext.newVersion = System.getProperty('newVersion')
	println "Setting version to ${newVersion}."
	// String tokenization to support -SNAPSHOT
	ant.replaceregexp(file: 'build.gradle', match: '"opensearch.version", "\\d.*"', replace: '"opensearch.version", "' + newVersion.tokenize('-')[0] + '-SNAPSHOT"', flags: 'g', byline: true)
}
}

tasks.named('dependencyLicenses') {
enabled = false
}

tasks.named('thirdPartyAudit') {
enabled = false
}

spotless {
format 'misc', {
	// define the files to apply `misc` to
	target '*.gradle', '.gitattributes', '.gitignore'

	// define the steps to apply to those files
	trimTrailingWhitespace()
	leadingSpacesToTabs() // or leadingTabsToSpaces. Takes an integer argument if you don't like 4
	endWithNewline()
}
java {
	importOrder()
	removeUnusedImports()
	trimTrailingWhitespace()
	endWithNewline()
	// apply a specific flavor of google-java-format
	googleJavaFormat('1.32.0').aosp().reflowLongStrings().skipJavadocFormatting()
	// fix formatting of type annotations
	formatAnnotations()
	licenseHeader("/*\n" + " * Copyright OpenSearch Contributors\n" + " * SPDX-License-Identifier: Apache-2.0\n" + " */\n\n")
}

// This task copies the dependencies to a 'libs' directory
tasks.register('copyDependencies', Copy) {
	// this configuration is from plugin org.graalvm.python
	// it is discovered by ./gradlew dependencies
	from configurations.pythonLauncherClasspath // Collects all files, including transitive dependencies
	into pythonLauncherLibDir // Destination directory
}

// Task to download GraalVM compiler jar
tasks.register('downloadGraalCompiler') {
	def compilerFile = layout.buildDirectory.file("compiler.jar").get().asFile

	outputs.file(compilerFile)

	doLast {
		def compilerUrl = "https://repo1.maven.org/maven2/org/graalvm/compiler/compiler/${compilerVersion}/compiler-${compilerVersion}.jar"

		if (!compilerFile.exists()) {
			compilerFile.parentFile.mkdirs()
			println "Downloading GraalVM compiler from: ${compilerUrl}"
			new URL(compilerUrl).withInputStream { i ->
				compilerFile.withOutputStream { it << i }
			}
			println "Downloaded compiler.jar to: ${compilerFile.absolutePath}"
		} else {
			println "Compiler jar already exists: ${compilerFile.absolutePath}"
		}
	}
}

// Make assemble depend on downloadGraalCompiler
assemble.dependsOn downloadGraalCompiler

tasks.named('yamlRestTest', RestIntegTestTask) {
	dependsOn 'copyDependencies', 'downloadGraalCompiler'
}

testClusters.yamlRestTest {
	jvmArgs "--module-path=${pythonLauncherLibDir.get().asFile} --add-modules=${appendModuleNames} -XX:+UnlockExperimentalVMOptions -XX:+EnableJVMCI --upgrade-module-path=${layout.buildDirectory.file("compiler.jar").get().asFile} --enable-native-access=org.graalvm.truffle,ALL-UNNAMED -Dtruffle.class.path.append=${pythonLauncherLibDir.get().asFile}"
}

// Skip filepermissions check since the generated python binaries will be executable
tasks.named('filepermissions') {
	enabled = false
}

// Exclude ANTLR-generated code from javadoc warnings
tasks.named('javadoc') {
options.addStringOption('Xdoclint:all,-missing', '-quiet')
}

graalPy {
// optional: specify python packages to install. E.g. "numpy==1.24.3"
packages = ["numpy==2.2.4"]
// Using VFS (Virtual Filesystem) approach - resources embedded in JAR
resourceDirectory = "GRAALPY-VFS/${opensearch_group}/${pluginName}"
}
}
